module router_fsm(clock,resetn,pkt_valid,busy,parity_done,data_in,soft_reset_0,soft_reset_1,soft_reset_2,fifo_full,low_pkt_valid,fifo_empty_0,fifo_empty_1,fifo_empty_2,detect_add,
				ld_state,full_state,laf_state,write_enb_reg,rst_int_reg,lfd_state);
	input clock,resetn,pkt_valid,parity_done,soft_reset_0,soft_reset_1,soft_reset_2,fifo_full,low_pkt_valid,fifo_empty_0,fifo_empty_1,fifo_empty_2;
	input [1:0]data_in;
	output busy,detect_add,ld_state,full_state,laf_state,write_enb_reg,rst_int_reg,lfd_state;
	
	parameter 	DECODE_ADDRESS 	   = 3'b000,
				WAIT_TILL_EMPTY    = 3'b001,
				LOAD_FIRST_DATA	   = 3'b010,
				LOAD_DATA          = 3'b011,
				LOAD_PARITY        = 3'b100,
				FIFO_FULL_STATE    = 3'b101,
				LOAD_AFTER_FULL    = 3'b110,
				CHECK_PARITY_ERROR = 3'b111;
	
	reg [2:0]present_state,next_state;  //internal registers
	reg [1:0]addr;
always @(posedge clock)   //
begin
    if(!resetn)
        addr <= 2'b00;

    else if(detect_add)
        addr <= data_in;
end
//present state sequential block
always @(posedge clock)
begin
    if(!resetn)
        present_state <= DECODE_ADDRESS; // hard reset

    else if(((soft_reset_0) && (addr==2'b00)) ||((soft_reset_1) && (addr==2'b01)) ||((soft_reset_2) && (addr==2'b10)))

        present_state <= DECODE_ADDRESS;

    else
        present_state <= next_state;
end
 
// State Machine Next State combinational Logic
always @(*)
begin
	next_state = present_state;
    case(present_state)

    DECODE_ADDRESS:
    begin
        if((pkt_valid && (data_in==2'b00) && fifo_empty_0) || (pkt_valid && (data_in==2'b01) && fifo_empty_1) ||(pkt_valid && (data_in==2'b10) && fifo_empty_2))
            next_state = LOAD_FIRST_DATA;

        else if((pkt_valid && (data_in==2'b00) && !fifo_empty_0) ||(pkt_valid && (data_in==2'b01) && !fifo_empty_1) ||(pkt_valid && (data_in==2'b10) && !fifo_empty_2))
            next_state = WAIT_TILL_EMPTY;

        else
            next_state = DECODE_ADDRESS;
    end

    LOAD_FIRST_DATA:
    begin
        next_state = LOAD_DATA;
    end

    WAIT_TILL_EMPTY:
    begin
        if((fifo_empty_0 && (addr==2'b00)) ||(fifo_empty_1 && (addr==2'b01)) ||(fifo_empty_2 && (addr==2'b10)))
            next_state = LOAD_FIRST_DATA;
        else
            next_state = WAIT_TILL_EMPTY;
    end

    LOAD_DATA:
    begin
        if(fifo_full)
            next_state = FIFO_FULL_STATE;

        else if(!fifo_full && !pkt_valid)
            next_state = LOAD_PARITY;

        else
            next_state = LOAD_DATA;
    end

    FIFO_FULL_STATE:
    begin
        if(!fifo_full)
            next_state = LOAD_AFTER_FULL;
        else
            next_state = FIFO_FULL_STATE;
    end

    LOAD_AFTER_FULL:
    begin
        if(!parity_done && low_pkt_valid)
            next_state = LOAD_PARITY;

        else if(!parity_done && !low_pkt_valid)
            next_state = LOAD_DATA;

        else if(parity_done)
            next_state = DECODE_ADDRESS;

        else
            next_state = LOAD_AFTER_FULL;
    end

    LOAD_PARITY:
    begin
        next_state = CHECK_PARITY_ERROR;
    end

    CHECK_PARITY_ERROR:
    begin
        if(!fifo_full)
            next_state = DECODE_ADDRESS;
        else
            next_state = FIFO_FULL_STATE;
    end

    default:
        next_state = DECODE_ADDRESS;

    endcase
end
assign busy =(present_state==LOAD_FIRST_DATA) ||(present_state==LOAD_PARITY)||(present_state==FIFO_FULL_STATE) ||(present_state==LOAD_AFTER_FULL) ||(present_state==WAIT_TILL_EMPTY) ||
       (present_state==CHECK_PARITY_ERROR);

assign detect_add =
       (present_state==DECODE_ADDRESS);

assign lfd_state =
       (present_state==LOAD_FIRST_DATA);

assign ld_state =
       (present_state==LOAD_DATA);

assign write_enb_reg =
       (present_state==LOAD_DATA) ||(present_state==LOAD_AFTER_FULL) ||(present_state==LOAD_PARITY);

assign full_state =
       (present_state==FIFO_FULL_STATE);

assign laf_state =
       (present_state==LOAD_AFTER_FULL);

assign rst_int_reg =
       (present_state==CHECK_PARITY_ERROR);
endmodule
